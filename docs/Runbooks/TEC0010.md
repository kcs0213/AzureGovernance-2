### [TEC0010-ExportUsageData](https://raw.githubusercontent.com/fbodmer/AzureGovernance/master/TEC0010-ExportUsageData.ps1)

The Runbook exports the usage data of all Subscriptions (to which the technical users has access to) to a Log Analytics instance. Each Resource is stored with the related Resource Group and the accumulated cost per Resource. All Resource Groups with their Tags are also exported.

PowerShell modules that are used by this Runbook and likely not yet installed:
```powershell
Install-Module AzureRm.UsageAggregates, AzureRm.Consumption, Microsoft.ADAL.PowerShell
```

<img src="https://github.com/fbodmer/AzureGovernance/wiki/Runbooks/TEC0010.png" width="500"><br/><br/>


**Report with Accumulated Cost per Resource Group**<br/>

The following query is used in Log Analytics to create a report that details the accumulated cost by Resource Group. This report can be [exported to Power BI](https://docs.microsoft.com/en-us/azure/azure-monitor/platform/powerbi#export-query).

```powershell
//
// This query retrieves the latest data set that was written by the Runbook TEC0010-ExportUsageData into Log Analytics using the type CostMonitoring_CL
// and summarizes the cost on Resource Group level.
//
let LatestDataSet = CostMonitoring_CL                                              // Create table with latest data set written to OMS
| join kind=inner
(CostMonitoring_CL | project TimeGenerated | top 1 by TimeGenerated desc)          // Determine the TimeGenerated of the last set of data written to the workspace
on TimeGenerated;
LatestDataSet | summarize sum(toreal(Cost_s)) by ResourceGroupName_s
```
<br/>

**Report for Resource Groups over Budget**<br/>

Below query produces a diagram with all the Resource Groups which are over budget. This is a comparison between accumulated cost (month-to-date) and the value defined in the budget tag. 

```powershell
//
// This query retrieves the latest data set that was written by the Runbook TEC0010 into Log Analytics using the type CostMonitoring_CL
// The billing data for all resources are consolidated into on Resource Group level and compared to the 'Budget' Tag on the Resource Groups
// There might be a scenario where Resource data is retrieved but no corresponding Resource Group. This is the case if a Resource Group is deleted within a billing cycle. 
// The Resources are therefore not included in the report. This should not cause any issues since there is no cost control required for deleted resources.
//
let LatestDataSet = CostMonitoring_CL // Create table with latest data set written to Log Analytics
| join kind=inner
(CostMonitoring_CL | project TimeGenerated | top 1 by TimeGenerated desc) // Determine the TimeGenerated of the last set of data written to Log Analytics
on TimeGenerated
| project TimeGenerated, Budget_s, ResourceGroupName_s, ResourceName_s, Cost_s;
let ResourceGroups = LatestDataSet // Create table with Resource Groups - they have 'Budget' attribute
| where ResourceGroupName_s == ResourceName_s; // Are the same for Resource Groups
ResourceGroups // Join the two tables to get total cost per Resource Group and Tags
| join
(
LatestDataSet
| summarize sum(toreal(Cost_s)) by ResourceGroupName_s // Sum cost on Resource Group level
// | extend sum_Cost_s = round(sum_Cost_s, 2) // Round the total
)
on ResourceGroupName_s // Join on Resource Group name
| extend Budget_s = (toreal(Budget_s)) // Convert from string
| extend MonthToDatePlanned = round(Budget_s / 30 * dayofmonth(now()), 2) // Calculate what should have been consumed MTD
| extend PercentageUsed = round(sum_Cost_s * 100 / MonthToDatePlanned, 0) // Percentag of MTD budget used 
| extend InBudget = iff(PercentageUsed > 120, "No", "Yes") // Determine if in or out of budget
| where InBudget == "No" // Select out of budget Resources
| project ResourceGroupName_s, MonthToDatePlanned, sum_Cost_s // Select attributes for chart
| render barchart kind=unstacked // Render chart
```

